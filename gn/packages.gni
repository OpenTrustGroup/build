# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

declare_args() {
  # List of packages (a comma-separated string).  If unset, guessed based
  # on which layer is found in the //.jiri_manifest file.
  fuchsia_packages = []
}

if (fuchsia_packages == []) {
  _jiri_manifest = "//.jiri_manifest"
  foreach(layer,
          exec_script("//build/gn/guess_layer.py",
                      [ rebase_path(_jiri_manifest) ],
                      "list lines",
                      [ _jiri_manifest ])) {
    fuchsia_packages += [ "$layer/packages/default" ]
  }
}

# A list of strings in GN syntax is valid JSON too.
_preprocessed_packages = exec_script("prepreprocess_build_packages.py",
                                     [ "--packages=$fuchsia_packages" ],
                                     "scope",
                                     [ "package_imports_resolver.py" ])

packages_imported = _preprocessed_packages.imported
packages_labels = _preprocessed_packages.labels
