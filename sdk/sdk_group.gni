# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/sdk/sdk_molecule.gni")

# A group of objects that can be added to an SDK.

template("sdk_group") {
  main_target_name = target_name
  sdk_target_name = "${target_name}_sdk"

  group(main_target_name) {
    forward_variables_from(invoker, "*")

    if (defined(visibility)) {
      visibility += [ ":$sdk_target_name" ]
    }
  }

  sdk_molecule(sdk_target_name) {
    is_group = true

    deps = []
    if (defined(invoker.public_deps)) {
      foreach(dep, invoker.public_deps) {
        full_label = get_label_info(dep, "label_no_toolchain")
        deps += [ "${full_label}_sdk" ]
      }
    }

    non_sdk_deps = [ ":$main_target_name" ]
  }
}
