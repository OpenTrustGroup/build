# Copyright 2018 Open Trust Group
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

template("gzos_client_app") {
  app_name = target_name

  source_set("${app_name}_sources") {
    visibility = [
      ":${app_name}_bin",
    ]

    public_deps = [
        "//garnet/public/lib/gzos/client_app/cpp",
    ]
    public_configs = []
    forward_variables_from(invoker, "*")
  }

  executable("${app_name}_bin") {
    output_name = "${app_name}"

    deps = [
      ":${app_name}_sources"
    ]
  }

  package(target_name) {
    deprecated_system_image = true

    deps = [
      ":${app_name}_bin"
    ]

    binaries = [
      {
        name = "${app_name}"
      }
    ]
  }
}

template("gzos_server_app") {
  app_name = target_name

  assert(defined(invoker.manifest), "Must define manifest file path")
  manifest_path = invoker.manifest

  source_set("${app_name}_sources") {
    visibility = [
      ":${app_name}_bin",
    ]

    public_deps = [
        "//garnet/public/lib/fxl",
        "//garnet/public/lib/component/cpp",
        "//zircon/public/lib/fs",
        "//zircon/public/lib/async-loop-cpp",
    ]
    public_configs = []
    forward_variables_from(invoker, "*")
  }

  executable("${app_name}_bin") {
    output_name = "${app_name}"

    deps = [
      ":${app_name}_sources"
    ]
  }

  package(target_name) {
    deprecated_system_image = true

    resources = [
      {
        path = rebase_path(manifest_path)
        dest = "${app_name}/manifest.json"
      }
    ]

    deps = [
      ":${app_name}_bin"
    ]

    binaries = [
      {
        name = "${app_name}"
      }
    ]
  }
}
